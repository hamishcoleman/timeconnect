#!/usr/bin/perl -w
use warnings;
use strict;
#
# Attempt a connection to a remote host and time the response
#

our $VERSION="1.0";

use Socket;
use FileHandle;
use Time::HiRes;
use Getopt::Long;
use Pod::Usage;
use Carp;
use English;

my $options={
	count => 0,
	interval => 1,
	port => 80
};
GetOptions($options,
	"help|h|?",
	"man",
	"count|c=i",
	"interval|i=i",
	"port|p=i"
) or pod2usage(2);
if(defined $options->{help}){pod2usage(-exitstatus => 0, -verbose => 1)};
if(defined $options->{man}){pod2usage(-exitstatus => 0, -verbose => 2)};
if(!defined $ARGV[0]){pod2usage(-exitstatus => 0, -verbose => 0)};

my $count = $options->{count};
#I couldn't think of any other way to find out if 'count' had been /explictly/
#called from the commandline. '--count 1' is a valid request and assuming it as
#a special case screws that up (and causes silly maths during the incrementation
#process). This was previously set by 'if defined' but since we're explicitly
#defining it above, can no longer use that.
my $repeat = 0;
if($count == 0){
	$repeat = 1;
	$count = 1;
};
my $interval = $options->{interval};
my $iteration = 1;



my $rem_host = $ARGV[0];
my $rem_port = $options->{port};

$OUTPUT_AUTOFLUSH=1;

$rem_host || croak "Remote host not supplied";
$rem_port || croak "Remote port not supplied";


my $iaddr = inet_aton($rem_host);
my $paddr = sockaddr_in($rem_port, $iaddr);
my $proto = getprotobyname('tcp');
my $fh    = FileHandle->new;

sub checkconnect{
	my $time1 = Time::HiRes::time;
	if (!socket($fh, PF_INET, SOCK_STREAM, $proto)) {
		croak "socket error";
	}
	if (!connect($fh, $paddr)) {
		croak "connect error";
	}
	$fh->autoflush(1);
	my $elapsed = Time::HiRes::time - $time1;
	my $milliseconds = $elapsed*1000;
	# the "". part changes the output of localtime
	printf "%s $rem_host:$rem_port\t%s\t%.3g ms\n",
		"". localtime time, inet_ntoa($iaddr), $milliseconds;

	# easily allow only one probe per second
	# last iteration of a limited run should not delay return to prompt
	if ($iteration < $count || $repeat == 1){
		if ($elapsed < $interval) {
			Time::HiRes::sleep($interval-$elapsed);
		};
	};
	return;
};


while ($iteration <= $count){
	checkconnect();
	if($repeat == 0){
		$iteration++;
	};

};


0;
__END__
=head1 NAME

timeconnect - times connections to TCP socket

=head1 USAGE

timeconnect [--help] [--man] [--count X] [--interval X] [--port X] destination

timeconnect [-h] [-c X] [-i X] [-p X] destination 

timeconnect -c 20 -i 3 -p 80 192.168.1.1

timeconnect www.101spares.com

=head1 DESCRIPTION

Timeconnect attempts a tcp connection to the given destination and times the response. The destination can be in the form of an IP address or an FQDN and the port must also be declared.

The user can specify the time between attempts and the total count of connections to attempt. If these values are not specified, timeconnect will run continuously with a minimum interval of one second between attempts.

=head1 ARGUMENTS

Timeconnect takes the following arguments:

=over 4

=item B<--count, -c> Repeats the connection test for COUNT attempts. If this value is not specified, timeconnect will run continuously

=item B<--help, -h>	Displays the usage message

=item B<--interval, -i>	Sets the interval in seconds between connection tests. If this value is not specified, timeconnect will default to an interval of 1.

=item B<--port, -p> Sets the destination port to use. If this value is not specificed, timeconnect will default to a port of 80 (standard http)

=back

=head1 AUTHOR

Hamish Coleman (meat) and Paul Morahan (potatoes)

=head1 HISTORY

Paul made the mistake of submitting a trivial change and was invited to write the potatoes instead

=cut



